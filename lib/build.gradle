plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def libGroupId = "com.sd.lib.android"
def libArtifactId = "adapter"
def libVersionCode = 20210824
def libVersionName = "1.2.2-beta03"

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
        versionCode libVersionCode
        versionName libVersionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    api 'androidx.recyclerview:recyclerview:1.0.0'
    api 'com.github.zj565061763:selectmanager:latest.release'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = libGroupId
                artifactId = libArtifactId
                version = libVersionName

                // 上传源码
                artifact generateSourcesJar
            }
        }
    }
}